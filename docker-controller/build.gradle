buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id "com.ewerk.gradle.plugins.dagger" version "1.0.0"
}


group 'de.schub.docker_controller'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'docker'


repositories {
    mavenCentral()
    jcenter()
}

mainClassName = "de.schub.docker_controller.Main"

run {
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

docker {
    baseImage "develar/java:8u45"
    maintainer 'Florian Pfitzer "florian@pfitzer.me"'
}

distDocker {
    runCommand "apk --update add bash && rm -rf /tmp/* /var/cache/apk/*"
    exposePort 4041
}

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude "META-INF/*.RSA", "META-INF/*.SF","META-INF/*.DSA"
    with jar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.mockito:mockito-core:1.+"

    compile "com.google.code.gson:gson:2.3"

    compile "com.orbitz.consul:consul-client:0.9.3"
    //compile 'org.apache.cxf:cxf-rt-rs-client:3.0.3'
    //compile 'org.apache.cxf:cxf-rt-transports-http-hc:3.0.3'

    compile "com.spotify:docker-client:2.7.7"
    compile "org.slf4j:slf4j-simple:1.7.6"
    compile "commons-cli:commons-cli:1.3"
    compile "com.beust:jcommander:1.30"
}

test {
    test.systemProperties = System.properties
}